## Configure Copyright File for Debian Package
function( configure_pkg PACKAGE_NAME_T COMPONENT_NAME_T PACKAGE_VERSION_T MAINTAINER_NM_T MAINTAINER_EMAIL_T)
    if("${COMPONENT_NAME_T}" STREQUAL "asan")
      set(LINTIAN_DOCS_DIR "${CMAKE_INSTALL_DOCDIR}-asan")
    else()
      set(LINTIAN_DOCS_DIR ${CMAKE_INSTALL_DOCDIR})
    endif()

    # Check If Debian Platform
    find_file (DEBIAN debian_version debconf.conf PATHS /etc)
    if(DEBIAN)
      set( BUILD_DEBIAN_PKGING_FLAG ON CACHE BOOL "Internal Status Flag to indicate Debian Packaging Build" FORCE )
      set_debian_pkg_cmake_flags( ${PACKAGE_NAME_T} ${PACKAGE_VERSION_T}
                                  ${MAINTAINER_NM_T} ${MAINTAINER_EMAIL_T} )

      # Create debian directory in build tree
      file(MAKE_DIRECTORY "${MIVISIONX_BINARY_DIR}/DEBIAN")

      # Configure the copyright file
      configure_file(
        "${MIVISIONX_ROOT_DIR}/copyright.txt"
        "${MIVISIONX_BINARY_DIR}/DEBIAN/copyright.txt"
        @ONLY
      )

      # Install copyright file
      install ( FILES "${MIVISIONX_BINARY_DIR}/DEBIAN/copyright.txt"
      DESTINATION "${LINTIAN_DOCS_DIR}"
      COMPONENT ${COMPONENT_NAME_T} )

      # Configure the changelog file
      configure_file(
        "${MIVISIONX_ROOT_DIR}/CHANGELOG.md"
        "${MIVISIONX_BINARY_DIR}/DEBIAN/CHANGELOG.md"
        @ONLY
      )

      # Install Change Log 
      find_program ( DEB_GZIP_EXEC gzip )
      if(EXISTS "${MIVISIONX_BINARY_DIR}/DEBIAN/CHANGELOG.md" )
        execute_process(
          COMMAND ${DEB_GZIP_EXEC} -f -n -9 "${MIVISIONX_BINARY_DIR}/DEBIAN/CHANGELOG.md"
          WORKING_DIRECTORY "${MIVISIONX_BINARY_DIR}/DEBIAN"
          RESULT_VARIABLE result
          OUTPUT_VARIABLE output
          ERROR_VARIABLE error
        )
        if(NOT ${result} EQUAL 0)
          message(FATAL_ERROR "Failed to compress: ${error}")
        endif()

        install ( FILES "${MIVISIONX_BINARY_DIR}/DEBIAN/${DEB_CHANGELOG_INSTALL_FILENM}"
                  DESTINATION ${LINTIAN_DOCS_DIR}
                  COMPONENT ${COMPONENT_NAME_T})
      endif()

    else()
        # License file
        install ( FILES ${LICENSE_FILE}
            DESTINATION ${LINTIAN_DOCS_DIR} RENAME LICENSE.txt
            COMPONENT ${COMPONENT_NAME_T})
    endif()
endfunction()

# Set variables for changelog and copyright
# For Debian specific Packages 
function( set_debian_pkg_cmake_flags DEB_PACKAGE_NAME_T DEB_PACKAGE_VERSION_T DEB_MAINTAINER_NM_T DEB_MAINTAINER_EMAIL_T )
    # Setting configure flags
    set( DEB_PACKAGE_NAME             "${DEB_PACKAGE_NAME_T}" CACHE STRING "Debian Package Name" )
    set( DEB_PACKAGE_VERSION          "${DEB_PACKAGE_VERSION_T}" CACHE STRING "Debian Package Version String" )
    set( DEB_MAINTAINER_NAME          "${DEB_MAINTAINER_NM_T}" CACHE STRING "Debian Package Maintainer Name" )
    set( DEB_MAINTAINER_EMAIL         "${DEB_MAINTAINER_EMAIL_T}" CACHE STRING "Debian Package Maintainer Email" )
    set( DEB_COPYRIGHT_YEAR           "2025" CACHE STRING "Debian Package Copyright Year" )
    set( DEB_LICENSE                  "MIT" CACHE STRING "Debian Package License Type" )
    set( DEB_CHANGELOG_INSTALL_FILENM "CHANGELOG.md.gz" CACHE STRING "Debian Package ChangeLog File Name" ) 

    find_program( DEB_DATE_TIMESTAMP_EXEC date )
    set ( DEB_TIMESTAMP_FORMAT_OPTION "-R" )

    # Get TimeStamp
    if(NOT DEB_DATE_TIMESTAMP_EXEC)
      message(FATAL_ERROR "date command not found")
    endif()

    execute_process (
        COMMAND ${DEB_DATE_TIMESTAMP_EXEC} ${DEB_TIMESTAMP_FORMAT_OPTION}
        OUTPUT_VARIABLE TIMESTAMP_T
    )
    set( DEB_TIMESTAMP                "${TIMESTAMP_T}" CACHE STRING "Current Time Stamp for Copyright/Changelog" )

    message(STATUS "DEB_PACKAGE_NAME             : ${DEB_PACKAGE_NAME}" )
    message(STATUS "DEB_PACKAGE_VERSION          : ${DEB_PACKAGE_VERSION}" )
    message(STATUS "DEB_MAINTAINER_NAME          : ${DEB_MAINTAINER_NAME}" )
    message(STATUS "DEB_MAINTAINER_EMAIL         : ${DEB_MAINTAINER_EMAIL}" )
    message(STATUS "DEB_COPYRIGHT_YEAR           : ${DEB_COPYRIGHT_YEAR}" )
    message(STATUS "DEB_LICENSE                  : ${DEB_LICENSE}" )
    message(STATUS "DEB_TIMESTAMP                : ${DEB_TIMESTAMP}" )
    message(STATUS "DEB_CHANGELOG_INSTALL_FILENM : ${DEB_CHANGELOG_INSTALL_FILENM}" )
endfunction()
